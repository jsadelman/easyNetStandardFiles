create model drc

# LOAD DATABASES

# ## LETTER FEATURE REPRESENTATION
create dataframe_for_reps features_db
features_db load Databases/letter_features.eNd

# ## ORTHOGRAPHY, PHONOLOGY, FREQUENCIES
create dataframe_for_reps vocab
vocab load Databases/Vocab_files/vocabulary_full.eNd

# LEXICAL ROUTE LAYERS

# ## FEATURE LAYER
create representation feat_rep
feat_rep type blocked_representation
feat_rep set_element (features_db column_rep features)
feat_rep number 8

create iac_layer fl
fl represent feat_rep



# ## LETTER LAYER

create representation letter_rep
letter_rep type blocked_representation
letter_rep set_element (features_db column_rep letter)
letter_rep number 8

create iac_layer ll
ll represent letter_rep

# ## ORTHOGRAPHIC LEXICON (LAYER)
create iac_layer ol
ol represent (vocab column_rep orthography)

# ## PHONOLOGICAL LEXICON (LAYER)
create iac_layer pl
pl represent (vocab column_rep phonology)

# ## PHONEME LAYER (OUTPUT BUFFER)
create string_split phon_str
phon_str set_blank "
phon_str set_source (vocab column_rep phonology string_representation)
#orth_str set_min 8
phon_str create_block_keyed phon_str_bk

create iac_layer pml
#pml represent phoneme_chars
pml represent phon_str_bk

create bias_layer freql
freql represent (vocab column_rep orth_freq)

create bias_layer pfreql
pfreql represent (vocab column_rep phon_freq)

# LEXICAL ROUTE CONNECTIONS

# ## FEATURE-LETTER
create connection flc
flc autoconnect fl ll

create parallel_conversion lfpc
lfpc set_source letter_rep
lfpc set_target feat_rep

# ## LETTER-ORTH
create string_split orth_str
orth_str set_blank "
orth_str set_source (vocab column_rep orthography string_representation)
#orth_str set_min 8


create equiv_conversion letters
letters set_target (features_db column_rep letter string_representation)
letters set_source (orth_str inner) 

create equiv_conversion letters2
letters2 set_source (features_db column_rep letter string_representation)
letters2 set_target (orth_str inner) 

create parallel_conversion lopc
lopc set_source letter_rep
lopc set_target (orth_str get_target)

create parallel_conversion lopc2
lopc2 set_source (orth_str get_target)
lopc2 set_target letter_rep

include Models/lowerupper.eNs
((features_db column_rep letter) find_conversion (orth_str inner)) add_fallback caser

create connection loc
loc autoconnect ll ol
#loc parameter_info

# ## ORTH-LETTER
create connection olc
olc autoconnect ol ll

# ## ORTH-PHON
create connection opc
opc autoconnect ol pl
 
# ## PHON-ORTH
create connection poc
poc autoconnect pl ol

# ## PHON-PHONEME
create connection ppmc
ppmc autoconnect pl pml

# ## PHONEME-PHON
create connection pmpc
pmpc autoconnect pml pl

# ## ORTH-ORTH
create connection lato
lato autoconnect ol ol

# ## PHON-PHON
create connection latp
latp autoconnect pl pl

# ## PHONEME-PHONEME
create connection latpm
latpm autoconnect pml pml

create connection freqb
freqb autoconnect freql ol

create connection pfreqb
pfreqb autoconnect pfreql pl

#freqb details

# DECISION RULE

create serialize_blocks_observer thresholded_best_phonemes
thresholded_best_phonemes inner_type above_threshold_observer
thresholded_best_phonemes target pml
thresholded_best_phonemes rename_parameter threshold thresholded_best_phonemes_inner_threshold

#create verbose_observer vo
#create verbose_observer vo2
#create verbose_observer vo3
#vo target ll

create regex_observer pronouncer
pronouncer target thresholded_best_phonemes
pronouncer add ".* "
pronouncer add .*\?.* 
pronouncer add ^[^"]*$

drc define_output pronunciation pronouncer

#create time_observer time
#time target pml

# LDT

create above_threshold_observer lexical_decider
lexical_decider target ol
drc define_output lexical_decision lexical_decider





# ASSEMBLE MODEL


create string_representation pregraphemic
create string_representation graphemes

create flatten_string_conversion flat
flat set_source (orth_str get_target)
flat set_target pregraphemic


create binary_layer best_letters_bin
best_letters_bin represent letter_rep

create staggered_blocked_connection bester
bester attach ll
bester set_inner_type best_above_threshold_connection
bester set_target_inner_rep (features_db column_rep letter)
best_letters_bin attach bester


create string_layer best_letters_str
best_letters_str represent (orth_str get_target)

create string_layer pregl
pregl represent pregraphemic

create string_layer graph
graph represent graphemes

create string_layer phone
phone represent (phon_str get_target)

create binary_layer phone_bin
phone_bin represent phon_str_bk

create regex_conversion graphemize
graphemize set_source pregraphemic
graphemize set_target graphemes
create dataframe parse_graphemes_db
parse_graphemes_db load Databases/Vocab_files/RC99_parse_graphemes.eNd
parse_graphemes_db for_each_row graphemize add $search $replace $grapheme

create regex_conversion gpc
gpc set_target (phon_str get_target)
gpc set_source graphemes
create dataframe grapheme_phoneme_db
grapheme_phoneme_db load Databases/Vocab_files/RC99_grapheme_phoneme.eNd
grapheme_phoneme_db for_each_row gpc add $search $replace $phoneme

create connection graphemizer
graphemizer autoconnect pregl graph

create connection gpcer
gpcer autoconnect graph phone

create connection phonbin
phonbin autoconnect phone phone_bin


create connection nlpl
nlpl autoconnect phone_bin pml

create connection bestlls
bestlls autoconnect best_letters_bin best_letters_str
#bestlls set_inner_type 
#bestlls set_target_inner_rep (orth_str inner)
#bestlls attach best_letters_bin
#best_letters_str attach best_letters_bin

create connection blspg
blspg autoconnect best_letters_str pregl

drc define_input orthographic (vocab column_rep orthography string_representation) fl


#create steps cycle
#create group src
#src add fl ll flc ol loc olc pl opc poc ppmc pmpc pml lato latp latpm freql freqb thresholded_best_phonemes vo vo2 vo3 time pronouncer lexical_decider bester best_letters_bin best_letters_str bestlls pregl graph phone blspg gpcer graphemizer phone_bin nlpl pfreql pfreqb

# SET PARAMETERS

#create database psfile
#psfile load params

#create parameterset ps
#psfile for_each_row ps set $parameter $value
#src parameter_source ps

#loc details
#pmpc details

#flc parameter_info
#loc parameter_info
#ps get lato_inhib

# CREATE CYCLE TO RUN THE MODEL

#cycle add src step
#cycle add vo2 report
#cycle add vo3 report
#cycle add thresholded_best_phonemes report
#cycle add blahf write thresholded_best_phonemes pronouncer time
#cycle add blahf write (best_letters_str state) (pregl state) (graph state) (phone state) vo pronouncer time

#vo target ll

drc add thresholded_best_phonemes

include Models/extras.eNs
caser_plus add_fallback extras
((features_db column_rep features) find_conversion (orth_str inner)) add_fallback caser_plus
((features_db column_rep features) find_conversion (orth_str inner)) set_weight -1
